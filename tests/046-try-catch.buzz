import "std";

fun willFail() > void !> str {
    throw "Yolo";
}

fun partialCatch() > void !> str {
    try {
        willFail();

        throw 12;
    } catch (int _) {
        std.assert(false, message: "unreachable");
    }
}

fun returnFromCatch() > int {
    try {
        willFail();

        return 12;
    } catch (str _) {
        return 21;
    }

    return 31;
}

test "Try catch" {
    str setme = "";

    try {
        str _ = "i'm local to this try block";

        setme = "yes";

        willFail();

        partialCatch();

        std.assert(false, message: "unreachable");
    } catch (str error) {
        str _ = "a local in catch clause";

        std.assert(error == "Yolo", message: "caught error");
    } catch (int _) {
        std.assert(false, message: "unreachable");
    } catch {
        std.assert(false, message: "unreachable");
    }

    str afterLocal = "bye";

    std.assert(setme == "yes", message: "error was handled and code continues");

    std.assert(returnFromCatch() == 21, message: "return from catch clause works");

    std.assert(afterLocal == "bye", message: "catch closed its scope");
}

test "catch any catches everything" {
    var caught = false;
    try {
        willFail();
    } catch (any error) {
        caught = true;
        std.assert(error is str, message: "Could cath any error");
    }

    std.assert(caught, message: "Could catch any error");
}